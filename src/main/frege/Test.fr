module Test where

import punkt0.ScalaShow
import frege.test.QuickCheck
import lexer.Lexer(scanTokens, FileIterator)
import parser.Parser(parse)
import punkt0.lexer.Tokens (token,atomicliterals,keywords,types)
import punkt0.parser.Grammar hiding (token,wrap)
import punkt0.Positioned (initialPosition, Position)
import punkt0.parser.Pretty(prettyPrint)
import Data.Char(isPrint)

data NonEmptyString = S { !getNonEmpty :: String}

letters = unpacked "abcdefgijklmnopqrstuvxyzåäöπαABCDEFGHIJKLMNOPQRSTUVXYZ"
digits = unpacked "012345678"

data StringLiteral = SL {!value :: String}
instance Arbitrary StringLiteral where
    arbitrary = fmap (SL . packed) $ listOf strChar
        where
            -- assume we only have printable strings
            -- we can't have quotation marks or newlines in string literals
            -- because there is no escaping
            strChar = arbitrary `suchThat` (\c -> and [c /= '\n', c /= '"', isPrint c])
    shrink (SL "") = []
    shrink (SL str) = [SL ""]

data Letter = L {!value :: Char}
instance Arbitrary Letter where
    arbitrary = oneof (map pure ls)
        where
            ls :: [Letter]
            ls = fmap L letters

data AlphaNum = A {!value :: Char}
instance Arbitrary AlphaNum where
    arbitrary = oneof (map pure alphanums)
        where
            alphanums :: [AlphaNum]
            alphanums = fmap A $ letters ++ digits


instance Arbitrary NonEmptyString where
    arbitrary = do
        (L c) <- arbitrary :: Gen Letter
        alphanums <- listOf (arbitrary :: Gen AlphaNum)
        let chars = map AlphaNum.value alphanums
        let str = packed (c:chars)
        pure $ S str
    shrink (S str) = map (S . packed) . shrinkChars . unpacked $ str
        where
            -- should never happen, but let's make a case for it anyway
            shrinkChars [] = [['a']]
            -- keep the first char in the shrunk string
            shrinkChars (c:cs) = map (c :) $ shrinkList shrink cs


reserved :: [String]
--reserved = []
reserved = map fst atomicliterals ++ map fst keywords ++ map fst types

instance Arbitrary Identifier where
    arbitrary
        = do
            (S str) <- arbitrary :: Gen NonEmptyString
            if str `elem` reserved
             then arbitrary -- try again
             else pure $ ID str initialPosition
    shrink (ID str pos)
        = [ ID str' pos
        | (S str') <- shrink (S str)
        , not $ str' `elem` reserved
        ]

positiveInt :: Gen Int
positiveInt = fmap Positive.getPositive positive
    where
        positive = arbitrary :: Gen (Positive Int)

instance Arbitrary BinOp where
    arbitrary = oneof $ fmap pure [And, Or, Equals, LessThan, Plus, Minus, Times, Div]

instance Arbitrary Expression where
    arbitrary = sized arbExpr
        where
            simples =
                [ pure LitTrue <*> pos
                , pure LitFalse <*> pos
                , fmap LitInt positiveInt <*> pos
                , fmap LitString str <*> pos
                , fmap LitThis pos
                , fmap LitNull pos
                , fmap New identifier <*> pos
                , fmap Identifier identifier <*> pos
                ]
            arbExpr 0 = oneof $ simples
            arbExpr n = oneof $ simples ++
                [ fmap BinOp expression <*> op <*> expression <*> pos
                , fmap MethodCall expression <*> identifier <*> (listOf expression) <*> pos
                , fmap Not expression <*> pos
                , fmap Block (listOf1 expression) <*> pos
                , fmap Println expression <*> pos
                , fmap Assign identifier <*> expression <*> pos
                , While <$> expression <*> expression <*> pos
                , If <$> expression <*> expression <*> maybeExpression <*> pos
                ]
                where
                    expression = (arbExpr (n - 1)) :: Gen Expression
                    maybeExpression = oneof $ fmap Just expression : [pure Nothing]
            identifier = arbitrary :: Gen Identifier
            op = arbitrary :: Gen BinOp
            str = fmap StringLiteral.value (arbitrary :: Gen StringLiteral)
            pos :: Gen Position
            pos = pure initialPosition
    shrink (Identifier i pos) = []
    shrink (LitTrue pos) = []
    shrink (LitFalse pos) = []
    shrink (New i pos) = []
    shrink (LitThis pos) = []
    shrink (LitNull _) = []
    shrink (LitInt i pos) = []
    shrink (LitString s pos) = []
    shrink (BinOp left op right pos)
        = [left, right] ++ [BinOp e op e' pos | e <- shrink left, e' <- shrink right]
    shrink (MethodCall e i es pos)
        = e : es ++ [MethodCall obj i args pos | obj <- shrink e, args <- shrink es]
    shrink (Not e pos) =  e : [Not e' pos | e' <- shrink e]
    shrink (Block [] _) = []
    shrink (Block (e:es) pos) = e : es ++ [Block es' pos | (NonEmpty es') <- shrink (NonEmpty es)]
    shrink (Println e pos) = e : [Println p pos | p <- shrink e]
    shrink (Assign i e pos) = e : Identifier i pos : [Assign i e' pos | e' <- shrink e]
    shrink (While e e' pos) = e : e' : [While p b pos | p <- shrink e, b <- shrink e']
    shrink (If e e' elseBody pos)
        = e : e' : [If p b el pos | p <- shrink e, b <- shrink e', el <- shrink elseBody]

instance Arbitrary MethodDeclaration where
    arbitrary
        = do
        override <- arbitrary
        name <- arbitrary
        args <- resize 2 $ listOf arbitrary
        returnType <- arbitrary
        vars <- resize 2 $ listOf arbitrary
        exprs <- resize 10 $ listOf1 (resize 5 arbitrary)
        return $ MethodDeclaration override name args returnType vars exprs initialPosition
    shrink (MethodDeclaration override name args returnType vars exprs pos)
        = [ MethodDeclaration false name [] returnType [] exprs' pos
          | (NonEmpty exprs') <- shrink (NonEmpty exprs)]
          ++ [ MethodDeclaration override name args' returnType vars' exprs' pos
          | args' <- shrinkList shrink args
          , vars' <- shrinkList shrink vars
          , (NonEmpty exprs') <- shrink (NonEmpty exprs)
          ]

instance Arbitrary Type where
    arbitrary
        = do
        oneof $ (fmap User arbitrary) : (map pure [Bool, Int, String, Unit])

instance Arbitrary VarDeclaration where
    arbitrary
        = do
        name <- arbitrary
        varType <- arbitrary
        expr <- resize 5 arbitrary
        return $ Var name varType expr initialPosition
    shrink (Var name varType expr pos)
        = [Var name varType expr' pos | expr' <- shrink expr]

instance Arbitrary ClassDeclaration where
    arbitrary
        = do
        name <- arbitrary
        extends <- arbitrary
        vars <- resize 10 $ listOf arbitrary
        methods <- resize 5 $ listOf arbitrary
        return $ Class name extends vars methods initialPosition
    shrink (Class name extends vars methods pos)
        = Class name extends [] [] pos
          : [ Class name extends [] methods' pos
          | methods' <- shrinkList shrink methods]
          ++ [ Class name extends vars' methods' pos
          | vars' <- shrink vars
          , methods' <- shrinkList shrink methods]

instance Arbitrary MainDeclaration where
    arbitrary
        = do
        name <- arbitrary :: Gen Identifier
        extends <- arbitrary :: Gen Identifier
        body <- resize 5 $ listOf1 $ resize 5 arbitrary
        return $ Main name extends [] body initialPosition
    shrink (Main name extends vars body pos)
        = [ Main name extends [] body' pos
        | (NonEmpty body') <- shrink (NonEmpty body)
        ]
        ++ [Main name extends vars body' pos
        | vars <- shrinkList shrink vars
        , (NonEmpty body') <- shrink (NonEmpty body)
        ]

instance Arbitrary Program where
    arbitrary
        = do
            main <- arbitrary :: Gen MainDeclaration
            classes <- resize 4 $ listOf arbitrary
            return $ Program classes [] main initialPosition
    shrink (Program classes functions main pos)
        = [ Program [] [] main' pos
          | main' <- shrink main
          ]
          ++ [ Program classes' [] main' pos
          | classes' <- shrinkList shrink classes
          , main' <- shrink main
          ]

prop_pretty_expr :: Expression -> Bool
prop_pretty_expr ast =
    let
        code = prettyPrint ast
        parseTree = parseCode (expression <* tokenEOF) code
    in if Just ast == parseTree
        then true
        else if traceLn code
            then undefined
            else false

prop_pretty_method :: MethodDeclaration -> Bool
prop_pretty_method ast =
    let
        code = prettyPrint ast
        parseTree = parseCode (methodDeclaration <* tokenEOF) code
    in if Just ast == parseTree
        then true
        else if traceLn $ code ++ (unlines [scalashow ast, "\n\t/=\n", scalashow parseTree])
            then undefined
            else false

prop_pretty_program :: Program -> Bool
prop_pretty_program ast
 | otherwise =
    let
        code = prettyPrint ast
        parseTree = parseCode program code
    in if Just ast == parseTree
        then true
        else if traceLn $ code ++ (unlines [scalashow ast, "\n\t/=\n", scalashow parseTree])
            then undefined
            else false

check prop = quickCheckWith stdArgs.{maxSuccess = 500} . whenFail (System.exit 1) $ prop

main = do
    check (forAll (resize 5 arbitrary) prop_pretty_expr)
    check prop_pretty_method
    check prop_pretty_program

